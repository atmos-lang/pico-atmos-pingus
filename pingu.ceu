var gravity = -0.2

task Sprite (pos:XY, n, path) {
    var dim  :WH = pico-state-get-size-image(path)
    var dim' :WH = [dim.w/n, dim.h]

    var i
    par {
        set i = 0
        every 25:ms {
            set i = (i+dim'.w) % dim.w
        }
    } with {
        every :Pico.Draw {
            pico-state-set-image-crop([[i,0], dim'])
            pico-state-set-size-image(dim')
            pico-output-draw-image(pos, path)
        }
    }
}

task Pingu (pos :XY) {
    var spd :XY = [0,0]

    task Faller () { {:ret}
        defer {
            set spd.y = 0
        }
        spawn Sprite(pos, 8, "data/images/faller.png")
        every :Pico.Frame {
            if pos.y < -200 {
                throw(:ret)
            }
            set spd.y = spd.y + ((evt.ms*gravity) / 1000)
        }
    }

    task Walker () {
        while true {
            do {
                set spd.x = 0.05
                spawn Sprite(pos, 8, "data/images/right.png")
                await :Pico.Frame, (pos.x >= 320)
            }
            do {
                set spd.x = -0.05
                spawn Sprite(pos, 8, "data/images/left.png")
                await :Pico.Frame, (pos.x <= -320)
            }
        }
    }

    spawn {
        await spawn Faller ()
        await spawn Walker ()
    }

    every :Pico.Frame {
        set pos.x = pos.x + (evt.ms*spd.x)
        set pos.y = pos.y + (evt.ms*spd.y)
    }
}
