val pico = require "pico"

val Menu = @{}

func Menu.Button (pos, tit) {
    val img,dim = (ren,dim) where {
        sfc = IMG.load("data/images/menuitem.png") -> assert
        ren = REN::createTextureFromSurface(sfc) -> assert
        dim = totable('w','h',sfc::getSize())
    }
    val rect = pico.rect(pos, dim)
    spawn {
        every :Pico.Draw {
            REN::copy(img, nil, rect)
            pico.write(FNT, tit, pos)
        }
    }
    await(:Pico.Mouse.Button.Dn, pico.point_vs_rect(evt, rect))
}

func Menu.Main () {
    par_or {
        await Menu.Button(pico.pct_to_pos(25,25), "Story")
        :Menu_Story
    } with {
        await Menu.Button(pico.pct_to_pos(75,25), "Editor")
        :Menu_Editor
    } with {
        await Menu.Button(pico.pct_to_pos(25,50), "Levelsets")
        :Menu_Levelsets
    } with {
        await Menu.Button(pico.pct_to_pos(75,50), "Options")
        :Menu_Options
    } with {
        await Menu.Button(pico.pct_to_pos(50,75), "Exit")
        :Menu_Exit
    }
}

return(Menu)
