task Menu-Button (pos:XY, tit) {
    val dim = pico.get.image.size("data/images/menuitem.png")
    spawn {
        every :Pico.Draw {
            pico.output.draw.image(pos, "data/images/menuitem.png")
            pico.set.font("data/fonts/film-cryptic/Filmcryptic.ttf", 45)
            pico.output.draw.text(pos, tit)
        }
    }
    await(:Pico.Mouse.Button.Dn | pico.point-vs-rect?(it.pos, [pos,dim]))
}

task Main-Menu () {
    par-or {
        await spawn Menu-Button(pico.pct-to-pos(1/4, 1/4), "Story")
        :Menu-Story
    } with {
        await spawn Menu-Button(pico.pct-to-pos(3/4, 1/4), "Editor")
        :Menu-Editor
    } with {
        await spawn Menu-Button(pico.pct-to-pos(1/4, 2/4), "Levelsets")
        :Menu-Levelsets
    } with {
        await spawn Menu-Button(pico.pct-to-pos(3/4, 2/4), "Options")
        :Menu-Options
    } with {
        await spawn Menu-Button(pico.pct-to-pos(1/2, 3/4), "Exit")
        :Menu-Exit
    }
}
