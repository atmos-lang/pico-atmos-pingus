val pico = require "pico"

val Menu = @{}

func Menu.Button (pos, tit) {
    val img = ren where {
        sfc = IMG.load("data/images/menuitem.png") -> assert
        ren = REN::createTextureFromSurface(sfc) -> assert
    }
    val _,_,w,h = img::query()
    val rect = @{x=pos.x-(w/2), y=pos.y-(h/2), w=w, h=h}
    spawn {
        every :Pico.Draw {
            REN::copy(img, nil, rect)
            pico.write(FNT, tit, rect)
        }
    }
    await(:Pico.Mouse.Button.Dn, pico.point_vs_rect(evt, rect))
}

func Menu.Main () {
    par_or {
        await Menu.Button(pico.pct_to_pos(25,25), "Story")
        :Menu_Story
    } with {
        await Menu.Button(pico.pct_to_pos(75,25), "Editor")
        :Menu_Editor
    } with {
        await Menu.Button(pico.pct_to_pos(25,50), "Levelsets")
        :Menu_Levelsets
    } with {
        await Menu.Button(pico.pct_to_pos(75,50), "Options")
        :Menu_Options
    } with {
        await Menu.Button(pico.pct_to_pos(50,75), "Exit")
        :Menu_Exit
    }
}

return(Menu)
